set show advisories off .

load prelude.maude

fmod VAR is
    including QID .
    sort Var .
    subsort Qid < Var .
endfm

view Var from TRIV to VAR is
    sort Elt to Var .
endv

fmod EXPR is
    pr VAR .
    pr INT .
    pr BOOL .

    sorts Expr .
    subsort Var < Expr .
    subsort Int < Expr .
    subsort Bool < Expr .

    op _+_ : Expr Expr -> Expr [ditto] .
    op not_ : Expr -> Expr [ditto] .
    op _and_ : Expr Expr -> Expr [ditto] .
    op _=:=_ : Expr Expr -> Expr [prec 25] .
endfm

fmod SYMEXPR is
    pr INT .
    pr BOOL .

    sorts LInt LBool SymBool SymInt SymExpr .
    subsorts LBool < SymBool .
    subsorts LInt < SymInt .
    subsorts SymInt SymBool < SymExpr .

    op x(_): Nat -> LInt [ctor] .
    op b(_): Nat -> LBool [ctor] .
    op int _ : Int -> SymInt [ctor] .
    op bool _ : Bool -> SymBool [ctor] .

    op _+_ : SymInt SymInt -> SymInt [prec 20 assoc comm] .
    op not_ : SymBool -> SymBool .
    op _and_ : SymBool SymBool -> SymBool [prec 20 assoc comm] .
    op _=:=_ : SymExpr SymExpr -> SymBool [prec 25] .
endfm

view SymExpr from TRIV to SYMEXPR is
    sort Elt to SymExpr .
endv

fmod STATEMENTS is
    including VAR .
    including EXPR .

    sorts Statement Statements .
    subsort Statement < Statements .

    op _:=_ : Var Expr -> Statement [ctor] .
    op skip : -> Statement [ctor] .
    op if (_) { _ } else { _ } : Expr Statements Statements -> Statement [ctor] .
    op _;_ : Statements Statements -> Statements [ctor assoc id: skip prec 100] .
endfm

fmod THREAD is
    including NAT .
    including STATEMENTS .

    sort Thread .
    sort ThreadId .
    subsort Nat < ThreadId .

    op idle : -> Thread [ctor] .
    op <_|_> : ThreadId Statements -> Thread [ctor] .
endfm

view Thread from TRIV to THREAD is
    sort Elt to Thread .
endv

fmod SYMPATH is
    including EXPR .
    including SYMEXPR .
    including THREAD .
    including SET{Var} .

    sorts Vars Event SymPath .

    subsort Event < SymPath .
    subsort Set{Var} < Vars .

    op epsilon : -> SymPath [ctor] .
    op _._ : SymPath SymPath -> SymPath [ctor assoc id: epsilon] .
    op _(_,_,_) : ThreadId SymExpr Vars Vars -> Event [ctor] .

    var V : Var .
    vars E1 E2 : Expr .

    op vars : Expr -> Vars .
    eq vars(V) = V .
    eq vars(E1 + E2) = vars(E1), vars(E2) .
    eq vars(not(E1)) = vars(E1) .
    eq vars(E1 and E2) = vars(E1), vars(E2) .
    eq vars(E1 =:= E2) = vars(E1), vars(E2) .
    eq vars(E1) = empty [owise] .

    --- Canonicalization of sympath
    vars Ev1 Ev2 : Event .
    vars Sp1 Sp2 : SymPath .

    op canonical : SymPath -> SymPath .
    ceq canonical(Sp1 . Ev1 . Ev2 . Sp2) = canonical(Sp1 . Ev2 . Ev1 . Sp2)
      if permutable(Ev1, Ev2) /\ prec(Ev2, Ev1) .
    eq canonical(Sp1) = Sp1 [owise] .

    vars I I' : ThreadId .
    vars Se Se' : SymExpr .
    vars R R' W W' : Vars .

    op permutable : Event Event -> Bool .
    eq permutable(I (Se, W, R), I' (Se', W', R')) =
        I =/= I'
        --- Can we omit the write sets?
        and intersection(W, union(W', R')) == empty
        and intersection(W', union(W, R)) == empty .

    op prec : Event Event -> Bool .
    eq prec(I (Se, W, R), I' (Se', W', R')) = I < I' .

    op prefix : SymPath SymPath -> Bool .
    eq prefix(Sp1, Sp1 . Sp2) = true .
    eq prefix(Sp1, Sp2) = false [owise] .
endfm

fmod EVAL is
    including VAR .
    including EXPR .
    including SYMEXPR .
    including MAP{Var, SymExpr} .

    sort Substitution .
    subsort Map{Var, SymExpr} < Substitution .

    var M : Substitution .
    var N : Int .
    var B : Bool .
    var V : Var .
    vars E1 E2 : Expr .

    op eval : Substitution Expr -> SymExpr .
    eq eval(M, N) = int N .
    eq eval(M, B) = bool B .
    eq eval(M, V) = M[V] .
    eq eval(M, E1 + E2) = eval(M, E1) + eval(M, E2) .
    eq eval(M, not(E1)) = not(eval(M, E1)) .
    eq eval(M, E1 and E2) = eval(M, E1) and eval(M, E2) .
    eq eval(M, E1 =:= E2) = eval(M, E1) =:= eval(M, E2) .
endfm

fmod SEARCH-STATE is
    including EVAL .
    including SYMPATH .
    including SET{Thread} .

    sorts SearchState Pool NePool .
    subsort Set{Thread} < Pool .
    subsort NeSet{Thread} < NePool .

    op (|_,_,_,_|) : Thread Substitution SymPath Pool -> SearchState
        [ctor format (n d n s n s n s d n)] .
endfm

mod SEMANTICS is
    including CONFIGURATION .
    including SEARCH-STATE .

    subsort SearchState < Configuration .

    vars Sigma Sigma' : Substitution .
    vars V : Var .
    vars E : Expr .
    vars Ev1 Ev2 : Event .
    vars Se : SymExpr .
    vars P P' : Pool .
    vars NP NP' : NePool .
    vars Ss Ss' Ss'' : Statements .
    vars T T' : Thread .
    vars I I' : ThreadId .
    vars Sp Sp' : SymPath .

    op schedules : Pool SearchState -> Configuration .
    eq schedules(empty, (| idle, Sigma, Sp, P' |)) = none .
    eq schedules((T, P), (| idle, Sigma, Sp, P' |)) =
        (| T, Sigma, Sp, P' \ T |) schedules(P, (| idle, Sigma, Sp, P' |)) [owise] .

    crl [Assignment] :
        (| < I | V := E ; Ss >, Sigma, Sp, P |)
      =>
        (| idle, insert(V, Se, Sigma), Sp . Ev1, (P, < I | Ss >) |)
      if Se := eval(Sigma, E) /\
         Ev1 := I (bool true, V, vars(E)) .

    rl [Schedule] :
        (| idle, Sigma, Sp, NP |)
      =>
        schedules(NP, (| idle, Sigma, Sp, NP |)) .

    crl [Cond] :
        (| < I | if (E) { Ss } else { Ss' } ; Ss'' >, Sigma, Sp, P |)
        =>
          (| idle, Sigma, Sp . Ev1, (P, < I | Ss ; Ss'' > ) |)
          (| idle, Sigma, Sp . Ev2, (P, < I | Ss' ; Ss'' > ) |)
        if Ev1 := I (eval(Sigma, E), empty, vars(E)) /\
           Ev2 := I (eval(Sigma, not(E)), empty, vars(E)) .

    eq [Termination] :
        (| T, Sigma, Sp, (P, < I | skip >) |)
      =
        (| T, Sigma, Sp, P |)  .

    ceq [POR] :
        (| idle, Sigma, Sp, P |)
        (| idle, Sigma', Sp', P' |)
      =
        (| idle, Sigma, Sp, P |)
      if prefix(canonical(Sp'), canonical(Sp)) .
endm
